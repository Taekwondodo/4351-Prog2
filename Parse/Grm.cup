package Parse;

action code {: static Symbol.Symbol sym(String s) {
	         return Symbol.Symbol.symbol(s);
	        }
	    :};

parser code  {: 
  Lexer lexer;

  public void syntax_error(java_cup.runtime.Symbol current) {
   report_error("Syntax error (" + current.sym + ")", current);
  }

  ErrorMsg.ErrorMsg errorMsg;

  public void report_error(String message, java_cup.runtime.Symbol info) {
      errorMsg.error(info.left, message);
  }

  public Grm(Lexer l, ErrorMsg.ErrorMsg err) {
    this();
    errorMsg=err;
    lexer=l;
  }
:};

scan with {: return lexer.nextToken(); :};

terminal String ID, STRING;
terminal Integer INT;
terminal COMMA, COLON, SEMICOLON, LPAREN, RPAREN, LBRACK, RBRACK, 
	LBRACE, RBRACE, DOT, PLUS, MINUS, TIMES, DIVIDE, EQ, NEQ, LT, LE, 
	GT, GE, AND, OR, ASSIGN, ARRAY, IF, THEN, ELSE, WHILE, FOR, TO,
	DO, LET, IN, END, OF, BREAK, NIL, FUNCTION, VAR, TYPE;

non terminal Absyn.Exp          Exp, OpExp, LetExp, IfExp, RecordExp, SeqExp, CallExp, AssignExp;
non terminal Absyn.Var          Var, Lvalue;
non terminal Absyn.Dec			Dec, TypeDec, VarDec, FunctionDec;
non terminal Absyn.DecList		DecList;
non terminal Absyn.FieldList	FieldList;
non terminal Absyn.FieldExpList	FieldExpList;
non terminal Absyn.ExpList	    ExpList, SeqList;
non terminal Absyn.Ty			Ty, NameTy, RecordTy, ArrayTy;

precedence left ELSE;
precedence left OR;
precedence left AND;
precedence nonassoc EQ,NEQ,LT,LE,GT,GE;
precedence left PLUS,MINUS;
precedence left TIMES, DIVIDE;
precedence left LBRACK;

start with Exp;

Exp ::= ID:i			{: RESULT = new Absyn.VarExp(ileft, new Absyn.SimpleVar(ileft, sym(i))); :}
	| STRING:s		{: RESULT = new Absyn.StringExp(sleft, s); :}
	| INT:i 		{: RESULT = new Absyn.IntExp(ileft,i.intValue()); :}
	| NIL:n			{: RESULT = new Absyn.NilExp(nleft); :}
	| Lvalue:l 	  	{: RESULT = new Absyn.VarExp(lleft, l); :}
	| OpExp:o		{: RESULT = o; :}
	| AssignExp:a	{: RESULT = a; :}
	| CallExp:c 	{: RESULT = c; :}
	| LetExp:l		{: RESULT = l; :}	
	| IfExp:i		{: RESULT = i; :}
	| RecordExp:r	{: RESULT = r; :}
	| SeqExp:s		{: RESULT = s; :}
	| WHILE:w Exp:e1 DO Exp:e2	{: RESULT = new Absyn.WhileExp(wleft, e1, e2); :}
	| ID:i LBRACK Exp:e1 RBRACK OF Exp:e2 {: RESULT = new Absyn.ArrayExp(ileft, sym(i), e1, e2); :}
	| BREAK:b 		{: RESULT = new Absyn.BreakExp(bleft); :}
	| FOR ID:i ASSIGN Exp:e1 TO Exp:e2 DO Exp:e3	{: RESULT = new Absyn.ForExp(ileft,
															new Absyn.VarDec(ileft, sym(i), null, e1), e2, e3); :}
	| error:e 	{: RESULT = new Absyn.SeqExp(eleft, null); :}
	;

AssignExp ::= Lvalue:l ASSIGN Exp:e	{: RESULT = new Absyn.AssignExp(lleft, l, e); :}
			| ID:i ASSIGN Exp:e		{: RESULT = new Absyn.AssignExp(ileft, 
												new Absyn.SimpleVar(ileft, sym(i)), e); :}
	;

Lvalue ::= ID:i1 DOT ID:i2 					{: RESULT = new Absyn.FieldVar(i1left, 
														new Absyn.SimpleVar(i1left, sym(i1)), sym(i2)); :}
		 | ID:i LBRACK Exp:e RBRACK			{: RESULT = new Absyn.SubscriptVar(ileft, 
		 												new Absyn.SimpleVar(ileft, sym(i)), e); :}
		 | Lvalue:l LBRACK Exp:e RBRACK 	{: RESULT = new Absyn.SubscriptVar(lleft, l, e); :}
		 | Lvalue:l DOT ID:i 				{: RESULT = new Absyn.FieldVar(lleft, l, sym(i)); :}
	;


CallExp ::= ID:i LPAREN RPAREN 				{: RESULT = new Absyn.CallExp(ileft, sym(i), null); :}
		  | ID:i LPAREN ExpList:el RPAREN 	{: RESULT = new Absyn.CallExp(ileft, sym(i), el); :}
	;

ExpList ::= Exp:e					{: RESULT = new Absyn.ExpList(e, null); :}
		  | Exp:e COMMA ExpList:el 	{: RESULT = new Absyn.ExpList(e, el); :}
	;

SeqExp ::= LPAREN:r RPAREN				{: RESULT = new Absyn.SeqExp(rleft, null); :}
		 | LPAREN:r SeqList:sl RPAREN	{: RESULT = new Absyn.SeqExp(rleft, sl); :}
	;

LetExp ::= LET:l DecList:d IN END			{: RESULT = new Absyn.LetExp(lleft, d, null); :}
		 | LET:l DecList:d IN SeqExp:s END	{: RESULT = new Absyn.LetExp(lleft, d, s); :}
	;

IfExp ::= IF:i Exp:e1 THEN Exp:e2				{: RESULT = new Absyn.IfExp(ileft, e1, e2); :}
		| IF:i Exp:e1 THEN Exp:e2 ELSE Exp:e3	{: RESULT = new Absyn.IfExp(ileft, e1, e2, e3); :}
	;

RecordExp ::= ID:i LBRACE RBRACE {: RESULT = new Absyn.RecordExp(ileft, sym(i), null); :}
			| ID:i LBRACE FieldExpList:f RBRACE {: RESULT = new Absyn.RecordExp(ileft, sym(i), f); :}
	;

SeqList ::= Exp:e SEMICOLON SeqList:el 	{: RESULT = new Absyn.ExpList(e, el); :}
		  | Exp:e 					   	{: RESULT = new Absyn.ExpList(e, null); :}
	;

FieldExpList ::= ID:i EQ Exp:e 						{: RESULT = new Absyn.FieldExpList(ileft, sym(i), e, null); :}
			   | ID:i EQ Exp:e COMMA FieldExpList:f	{: RESULT = new Absyn.FieldExpList(ileft, sym(i), e, f); :}
	;

OpExp ::= Exp:e1 OR Exp:e2		{: RESULT = new Absyn.IfExp(e1left,e1, new Absyn.IntExp(e1left,1),e2);:}
		| Exp:e1 AND Exp:e2		{: RESULT = new Absyn.IfExp(e1left,e1,e2, new Absyn.IntExp(e1left,0)); :}
		| Exp:e1 EQ Exp:e2		{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.EQ,e2); :}
		| Exp:e1 LT Exp:e2 		{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.LT,e2); :}
		| Exp:e1 LE Exp:e2 		{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.LE,e2); :}
		| Exp:e1 GT Exp:e2		{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.GT,e2); :}
		| Exp:e1 GE Exp:e2		{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.GE,e2); :}
		| Exp:e1 NEQ Exp:e2		{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.NE,e2); :}
		| Exp:e1 PLUS Exp:e2	{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.PLUS,e2); :}
		| Exp:e1 MINUS Exp:e2	{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.MINUS,e2); :}
		| Exp:e1 TIMES Exp:e2	{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.MUL,e2); :}
		| Exp:e1 DIVIDE Exp:e2	{: RESULT = new Absyn.OpExp(e1left,e1,Absyn.OpExp.DIV,e2); :}
		| MINUS:n Exp:e 		{: RESULT = new Absyn.OpExp(nleft,
								   new Absyn.IntExp(nleft,0),Absyn.OpExp.MINUS,e); :}
		;	

DecList ::= Dec:d 				{: RESULT = new Absyn.DecList(d, null); :}
		  | Dec:d DecList:dl	{: RESULT = new Absyn.DecList(d, dl); :}
		  ;

Dec ::= TypeDec:t 				{: RESULT = t; :}
	  | VarDec:v				{: RESULT = v; :}
	  | FunctionDec:f 			{: RESULT = f; :}
	  ;

VarDec ::= VAR:v ID:i ASSIGN Exp:e				{: RESULT = new Absyn.VarDec(vleft,sym(i),null,e); :}
	     | VAR:v ID:i COLON ID:i2 ASSIGN Exp:e	{: RESULT = new Absyn.VarDec(vleft,sym(i),
		 										   new Absyn.NameTy(i2left,sym(i2)),e); :}
		 ;

FunctionDec ::= FUNCTION:n ID:i LPAREN RPAREN EQ Exp:e
				{: RESULT = new Absyn.FunctionDec(nleft,sym(i),null,null,e,null); :}
			  |FUNCTION:n ID:i1 LPAREN RPAREN COLON ID:i2 EQ Exp:e
				{: RESULT = new Absyn.FunctionDec(nleft,sym(i1),null,
					new Absyn.NameTy(i2left,sym(i2)),e,null); :}
			  | FUNCTION:n ID:i LPAREN FieldList:f RPAREN EQ Exp:e 
				{: RESULT = new Absyn.FunctionDec(nleft,sym(i),f,null,e,null); :}
			  | FUNCTION:n ID:i LPAREN FieldList:f RPAREN COLON ID:i2 EQ Exp:e 
				{: RESULT = new Absyn.FunctionDec(nleft,sym(i),f,new Absyn.NameTy(i2left,sym(i2)),e,null); :}
			;

FieldList ::= ID:i COLON ID:i2 {: RESULT = new Absyn.FieldList(ileft,sym(i),sym(i2),null); :}
		    | ID:i COLON ID:i2 COMMA FieldList:f {: RESULT = new Absyn.FieldList(ileft,sym(i),sym(i2),f); :}
			;

TypeDec ::= TYPE ID:i EQ Ty:t  {: RESULT = new Absyn.TypeDec(ileft,sym(i),t,null); :}
			;
						
Ty ::= ID:i					{: RESULT = new Absyn.NameTy(ileft, sym(i)); :}
	 | LBRACE FieldList:f RBRACE {: RESULT = new Absyn.RecordTy(fleft, f); :}
	 | ARRAY OF ID:i			{: RESULT = new Absyn.ArrayTy(ileft, sym(i)); :}
	 ;





















